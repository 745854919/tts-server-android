plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlinx-serialization'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id("com.google.devtools.ksp")
}

android {
    compileSdk 33
    namespace 'com.github.jing332.tts_server_android'
    defaultConfig {
        applicationId 'com.github.jing332.tts_server_android'
        minSdk 21
        targetSdk 33
        versionCode 8
        versionName "0.8"

        ksp{
            arg("room.schemaLocation",  "$projectDir/schemas".toString())
            arg("room.incremental", "true")
            arg("room.expandProjection", "true")
        }

        // 读取strings.xml所在文件夹 获得应用支持的语言
        tasks.register('buildTranslationArray') {
            def defaultCode = "zh-CN"
            def foundLocales = new StringBuilder()
            foundLocales.append("new String[]{")

            fileTree("src/main/res").visit { details ->
                if (details.file.path.endsWith("strings.xml")) {
                    def path = details.file.parent.replaceAll('\\\\', "/")
                    def languageCode = path.tokenize('/').last().replaceAll('values-', '').replaceAll('-r', '-')
                    languageCode = (languageCode == "values") ? defaultCode : languageCode;
                    foundLocales.append("\"").append(languageCode).append("\"").append(",")
                }
            }
            foundLocales.append("}")
            def foundLocalesString = foundLocales.toString().replaceAll(',}', '}')
            buildConfigField "String[]", "TRANSLATION_ARRAY", foundLocalesString
        }
        preBuild.dependsOn buildTranslationArray

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            //签名文件 从local.properties取值
            Properties pro = new Properties()
            InputStream input = project.rootProject.file("local.properties").newDataInputStream()
            pro.load(input)
            storeFile file(pro.getProperty("KEY_PATH"))
            storePassword pro.getProperty("KEY_PASSWORD")
            keyAlias pro.getProperty("ALIAS_NAME")
            keyPassword pro.getProperty("ALIAS_PASSWORD")
        }
    }

    buildTypes {
        release {
            versionNameSuffix "_${releaseTime()}"
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.release
            applicationIdSuffix ".debug"
            versionNameSuffix "_debug"
            splits.abi.enable = false
            splits.density.enable = false
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/INDEX.LIST', 'META-INF/*.md']
        }
    }


    // 分别打包APK 原版 和 dev共存版
    flavorDimensions += "version"
    productFlavors {
        app {
            dimension = "version"
        }
        dev {
            dimension = "version"
            applicationIdSuffix ".dev"
        }
    }

    splits {
        abi {
            enable true
            reset()

            include 'x86_64', 'x86', 'armeabi-v7a', 'arm64-v8a'
            universalApk true
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    kotlin {
        jvmToolchain(17)
    }

    // 修改apk文件名
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            //noinspection GrDeprecatedAPIUsage
            def abiName = output.getFilter(com.android.build.OutputFile.ABI)
            if (abiName == null)
                output.outputFileName = "TTS-Server-v${variant.versionName}.apk"
            else
                output.outputFileName = "TTS-Server-v${variant.versionName}_${abiName}.apk"
        }

    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }
}

dependencies {
    coreLibraryDesugaring('com.android.tools:desugar_jdk_libs:2.0.3')

    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.6.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    //noinspection GradleDependency
    implementation 'com.google.android.material:material:1.9.0-beta01'

    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.0'

    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'

    // RecyclerView
    //noinspection GradleDependency
    implementation 'com.github.liangjingkanji:BRV:1.3.90' // BRV 新版有拖动问题
    implementation "androidx.recyclerview:recyclerview:1.3.0"
    implementation 'me.zhanghai.android.fastscroll:library:1.2.0'

    implementation 'com.github.kirich1409:viewbindingpropertydelegate-noreflection:1.5.9'

    // Code Editor
    implementation 'io.github.Rosemoe.sora-editor:editor:0.21.1'
    implementation 'io.github.Rosemoe.sora-editor:language-textmate:0.21.1'

    // Room
    implementation("androidx.room:room-runtime:$room_version")
    implementation("androidx.room:room-ktx:$room_version")
    ksp("androidx.room:room-compiler:$room_version")
    androidTestImplementation("androidx.room:room-testing:$room_version")


    // IO & NET
    implementation 'com.squareup.okio:okio:3.3.0'
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    implementation 'com.github.liangjingkanji:Net:3.5.8'

    implementation 'me.rosuh:AndroidFilePicker:0.8.3'
    implementation 'com.louiscad.splitties:splitties-systemservices:3.0.0'

    implementation 'com.chibatching.kotpref:kotpref:2.13.2'
    implementation 'com.chibatching.kotpref:enum-support:2.13.2'

    implementation 'cn.hutool:hutool-crypto:5.8.17'

    // Media
    implementation 'com.google.android.exoplayer:exoplayer-core:2.19.0'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.19.0'

    // Logcat (Debug)
    debugImplementation 'com.github.getActivity:Logcat:11.2'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

static def releaseTime() {
    return new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("GMT+08:00"))
}
